apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//ext {
//    PUBLISH_GROUP_ID = 'com.mrbbot'
//    PUBLISH_ARTIFACT_ID = 'nicycler'
//    PUBLISH_VERSION = '1.0.1'
//}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "android.support.accept.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:recyclerview-v7:26.1.0'

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.mrbbot'
version = '1.0.1'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Nicycler'
            description 'A nicer version of Android\'s RecyclerView'
            url 'https://github.com/mrbbot/nicycler'
            inceptionYear '2018'

            packaging 'aar'
            groupId 'com.mrbbot'
            artifactId 'nicycler'
            version '1.0.1'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/mrbbot/nicycler.git'
                url 'https://github.com/mrbbot/nicycler'

            }
            developers {
                developer {
                    id = 'mrbbot'
                    name 'MrBBot'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'nicycler'
        userOrg = 'mrbbot'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mrbbot/nicycler.git'
        publish = true
        version {
            name = '1.0.1'
            desc = 'Nicycler 1.0.1'
            released = new Date()
            vcsTag = 'v1.0.1'
        }
    }
}

//apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'